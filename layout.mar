^include(@/pico/pico.mar)

func layout_to_rects {{w:Int,h:Int}}: (lay: #[Int*{{w}}*{{h}}], parent: Rect) -> #[Rect*({{w}}*{{h}})] {
    var tmp: #[Rect*({{w}}*{{h}})]
    set #tmp = 0
    do {
        loop l in {{h}} {
            loop c in {{w}} {
                if lay[(l*{{w}})+c] == #tmp {
                    var r = Rect [Pos[c,l], Dim[0,0]]
                    loop xc in ({{w}}-c) {
                        var xxc = xc + c
                        until(lay[(l*{{w}})+xxc] != #tmp)
                        set r.dim.x = (xxc - c) + 1
                    }
                    loop xl in ({{h}}-l) {
                        var xxl = xl + l
                        until(lay[(xxl*{{w}})+c] != #tmp)
                        set r.dim.y = (xxl - l) + 1
                    }
                    set tmp[#tmp] = r
                    set #tmp = #tmp + 1
                }
            }
        }
    }

    var ret: #[Rect*({{w}}*{{h}})]
    set #ret = 0
    do {
        var p = Pos [parent.dim.x/{{w}}, parent.dim.y/{{h}}]
        loop i in #tmp {
            var r = tmp[i]
            set ret[#ret] = Rect [Pos[x,y], Dim[w,h]] where {
                w = r.dim.x * p.x
                h = r.dim.y * p.y
                x = parent.pos.x + (r.pos.x * p.x) + (w/2)
                y = parent.pos.y + (r.pos.y * p.y) + (h/2)
            }
            set #ret = #ret + 1
        }
    }
    return(ret)
}

func layout_draw: (rs: \#[Rect]) -> () {
    var s = pm_get_style()
    pm_set_style(`PICO_STROKE`)
    loop i in #rs\ {
        pm_output_draw_rect(rs\[i])
    }
    pm_set_style(s)
}

test {
    do {
        print("= 1 =")
        var lay = #[
            0, 1,
        ]
        var rs = layout_to_rects {{2,1}} (lay, Rect[Pos[0,0],Dim[200,100]])
        expect(rs == #[
            Rect [Pos[ 50,50], Dim[100,100]],
            Rect [Pos[150,50], Dim[100,100]],
        ])
    }
    do {
        print("= 2 =")
        var lay = #[
            0, 0, 1,
            0, 0, 1,
            2, 2, 3,
            4, 5, 3,
        ]
        var rs = layout_to_rects {{3,4}} (lay, Rect[Pos[0,0],Dim[300,400]])
        expect(rs == #[
            Rect [Pos[100,100], Dim[200,200]],
            Rect [Pos[250,100], Dim[100,200]],
            Rect [Pos[100,250], Dim[200,100]],
            Rect [Pos[250,300], Dim[100,200]],
            Rect [Pos[ 50,350], Dim[100,100]],
            Rect [Pos[150,350], Dim[100,100]]
        ])
    }
}
